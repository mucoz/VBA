Option Explicit

'Created by Mustafa Can Ozturk on 10.02.2021
'===========================================
'The functions below will be used for file operations
'It includes the most common functions
'It needs to be used in "Window" module
'Functions:
'ActivateWindow, CloseWindow, MinimizeWindow, MaximizeWindow, MoveWindow, ResizeWindow, GetAppNames
'Properties:
'GetActiveWindowTitle
'===========================================



'====API's to get application names=============
'#If Win64 Or VBA7 Then

'Declare PtrSafe Function GetClassName Lib "user32" Alias "GetClassNameA" (ByVal hwnd As Long, ByVal lpClassName As String, ByVal nMaxCount As Long) As Long
'Declare PtrSafe Function GetDesktopWindow Lib "user32" () As Long
'Declare PtrSafe Function GetWindow Lib "user32" (ByVal hwnd As Long, ByVal wCmd As Long) As Long
'Declare PtrSafe Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
'Declare PtrSafe Function GetWindowText Lib "user32" Alias "GetWindowTextA" (ByVal hwnd As Long, ByVal lpString As String, ByVal aint As Long) As Long

'#Else

Private Declare Function GetClassName Lib "user32" Alias "GetClassNameA" (ByVal hWnd As Long, ByVal lpClassName As String, ByVal nMaxCount As Long) As Long
Private Declare Function GetDesktopWindow Lib "user32" () As Long
Private Declare Function GetWindow Lib "user32" (ByVal hWnd As Long, ByVal wCmd As Long) As Long
Private Declare Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long) As Long
Private Declare Function GetWindowText Lib "user32" Alias "GetWindowTextA" (ByVal hWnd As Long, ByVal lpString As String, ByVal aint As Long) As Long
Private Declare Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Declare Function SendMessage Lib "user32" Alias "SendMessageA" (ByVal hWnd As Long, ByVal wMsg As Long, ByVal wParam As Long, lParam As Long) As Long
Private Declare Function ShowWindow Lib "user32" (ByVal hWnd As Long, ByVal nCmdShow As Long) As Long
Private Declare Function SetActiveWindow Lib "user32" (ByVal hWnd As Long) As Long
Private Declare Function GetForegroundWindow Lib "user32" () As Long
Private Declare Function SetForegroundWindow Lib "user32" (ByVal hWnd As Long) As Long
Private Declare Function GetWindowTextLength Lib "user32" Alias "GetWindowTextLengthA" (ByVal hWnd As Long) As Long
Public Declare Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
Private Declare Function MoveWindow Lib "user32" (ByVal hWnd As Long, ByVal x As Long, ByVal y As Long, ByVal nWidth As Long, ByVal nHeight As Long, ByVal bRepaint As Long) As Long
Private Declare Function ShellExecute Lib "shell32.dll" Alias "ShellExecuteA" (ByVal hWnd As Long, ByVal lpOperation As String, ByVal lpFile As String, ByVal lpParameters As String, ByVal lpDirectory As String, ByVal nShowCmd As Long) As Long
Private Declare Function IsWindowVisible Lib "user32" (ByVal hWnd As Long) As Long
Private Declare Function IsWindowEnabled Lib "user32" (ByVal hWnd As Long) As Long

'#End If


'===================================================================================
'CONSTANTS
'These are the constants for Windows API's
Const GW_CHILD = 5
Const GW_HWNDNEXT = 2
Const GWL_STYLE = (-16)
Const WS_VISIBLE = &H10000000
Const MAX_LEN = 255

Const WM_CLOSE = &H10
Const WM_ACTIVATE = &H6
Const WM_SHOWWINDOW = &H18
Const WA_ACTIVE = 1
Const WM_ACTIVATEAPP = &H1C
Const WM_SETFOCUS = &H7
Const WM_ENABLE = &HA

' ShowWindow() Commands
Const SW_HIDE = 0
Const SW_SHOWNORMAL = 1
Const SW_NORMAL = 1
Const SW_SHOWMINIMIZED = 2
Const SW_SHOWMAXIMIZED = 3
Const SW_MAXIMIZE = 3
Const SW_SHOWNOACTIVATE = 4
Const SW_SHOW = 5
Const SW_MINIMIZE = 6
Const SW_SHOWMINNOACTIVE = 7
Const SW_SHOWNA = 8
Const SW_RESTORE = 9
Const SW_SHOWDEFAULT = 10
Const SW_MAX = 10

Sub main()

Sleep 3000
Debug.Print Apps.IsActive("PaintLetter")

End Sub


Public Property Get GetActiveAppName() As String
    
    GetActiveAppName = GetAppNameWithHandle(GetForegroundWindow)
    
End Property

Public Property Get GetActiveAppHandle() As Long

    GetActiveAppHandle = GetForegroundWindow
    
End Property

Public Property Get GetAppNameWithHandle(ByVal hWnd As Long) As String
    
    GetAppNameWithHandle = String(GetWindowTextLength(hWnd) + 1, Chr$(0))
    
    Call GetWindowText(hWnd, GetAppNameWithHandle, Len(GetAppNameWithHandle))
    
End Property

Public Function GetAppNames() As Variant
    
    Dim data As New Collection                '() As String
    Dim DataArr() As Variant
    Dim xStr As String
    Dim xStrLen As Long
    Dim xHandle As Long
    Dim xHandleStr As String
    Dim xHandleLen As Long, xHandleStyle As Long
    
    On Error Resume Next
    
    Dim i As Long
    
    i = 0
    xHandle = GetWindow(GetDesktopWindow(), GW_CHILD)
    
    Do While xHandle <> 0
        xStr = String$(MAX_LEN - 1, 0)
        xStrLen = GetWindowText(xHandle, xStr, MAX_LEN)
        If xStrLen > 0 Then
            xStr = Left$(xStr, xStrLen)
            xHandleStyle = GetWindowLong(xHandle, GWL_STYLE)
            If xHandleStyle And WS_VISIBLE Then
'                ReDim Preserve DataArr(i)
'                DataArr(i) = xStr
                data.add xStr, CStr(xStr)
'                i = i + 1
            End If
        End If
        xHandle = GetWindow(xHandle, GW_HWNDNEXT)
    Loop
    
    For i = 1 To data.Count

        ReDim Preserve DataArr(i - 1)

        DataArr(i - 1) = data.Item(i)

    Next i
    
    GetAppNames = DataArr

End Function

Public Sub PrintAppNames()
    
    Dim appList As Variant
    Dim i As Long
    
    appList = GetAppNames
    
    For i = LBound(appList) To UBound(appList)
    
        Debug.Print appList(i)
        
    Next i
    
End Sub

Public Function CloseWindow(WindowName As String) As Long

    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = SendMessage(hWnd, WM_CLOSE, 0, 0)
    
    CloseWindow = result
    
End Function

Public Function ActivateWindow(WindowName As String) As Long

    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = SetForegroundWindow(hWnd)
    
    ActivateWindow = result
    
End Function

Public Function MinimizeWindow(WindowName As String) As Long
    
    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = ShowWindow(hWnd, SW_MINIMIZE)
    
    MinimizeWindow = result
    
End Function

Public Function MaximizeWindow(WindowName As String) As Long

    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = ShowWindow(hWnd, SW_MAXIMIZE)
    
    MaximizeWindow = result
    
End Function

Public Function ResizeWindow(WindowName As String, PositionX As Long, PositionY As Long, Width As Long, Height As Long) As Long
    
    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = MoveWindow(hWnd, PositionX, PositionY, Width, Height, 0)
    
    ResizeWindow = result
    
End Function

Public Property Get GetHandle(WindowName As String) As Long
    
    Dim hWnd As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    GetHandle = hWnd
    
End Property

Public Property Get IsActive(WindowNameOrHandle As Variant) As Boolean

    Dim hWnd As Long
    Dim WindowName As String
    Dim status As Boolean
    
    If IsNumeric(WindowNameOrHandle) = True Then
    
        hWnd = WindowNameOrHandle
        
        If GetAppNameWithHandle(GetForegroundWindow) = GetAppNameWithHandle(hWnd) Then
        
            status = True
            
        Else
         
            status = False
            
        End If
        
        IsActive = status
        
    Else
    
        WindowName = WindowNameOrHandle
        hWnd = FindWindow(vbNullString, WindowName)
        
        If GetAppNameWithHandle(GetForegroundWindow) = GetAppNameWithHandle(hWnd) Then
        
            status = True
            
        Else
         
            status = False
            
        End If
        
        IsActive = status
        
    End If



End Property
Public Property Get IsVisible(WindowNameOrHandle As Variant) As Boolean

    Dim hWnd As Long
    Dim WindowName As String
    Dim status As Boolean
    
    If IsNumeric(WindowNameOrHandle) = True Then
    
        hWnd = WindowNameOrHandle
        status = IsWindowVisible(hWnd)
        IsVisible = status
        
    Else
    
        WindowName = WindowNameOrHandle
        hWnd = FindWindow(vbNullString, WindowName)
        status = IsWindowVisible(hWnd)
        IsVisible = status
        
    End If
    
End Property

Public Property Get IsOpen(WindowNameOrHandle As Variant) As Boolean
    
    Dim hWnd As Long
    Dim WindowName As String
    Dim status As Boolean
    
    If IsNumeric(WindowNameOrHandle) = True Then
    
        hWnd = WindowNameOrHandle
        status = IsWindowEnabled(hWnd)
        IsOpen = status
        
    Else
    
        WindowName = WindowNameOrHandle
        hWnd = FindWindow(vbNullString, WindowName)
        status = IsWindowEnabled(hWnd)
        IsOpen = status
        
    End If

End Property

Public Function FindAppNameFromText(PartialName As String) As String
    
    Dim list As Variant
    Dim i As Long
    
    list = GetAppNames
    
    For i = LBound(list) To UBound(list)
        
        If InStr(UCase(list(i)), UCase(PartialName)) > 0 Then
            
            FindAppNameFromText = list(i)
            Exit Function
        
        End If
        
    Next i
    
    FindAppNameFromText = "Not Found"
    
End Function

Public Function OpenApplication(WindowName As String, AppFullPath As String) As Long
    
    'Returns Application's Handle
    
    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = ShellExecute(hWnd, "open", AppFullPath, vbNullString, File.ParentFolder(AppFullPath), SW_NORMAL)
    
    If result = 2 Or result = 3 Then Exit Function
    
    Do
        DoEvents
        hWnd = FindWindow(vbNullString, WindowName)
    Loop Until hWnd > 0
    
    OpenApplication = hWnd
    
End Function

Public Function Hide(WindowName As String) As Long

    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = ShowWindow(hWnd, SW_HIDE)
    
    Hide = result
    
End Function

Public Function Show(WindowName As String) As Long
    
    Dim hWnd As Long
    Dim result As Long
    
    hWnd = FindWindow(vbNullString, WindowName)
    
    result = ShowWindow(hWnd, SW_SHOW)
    
    Show = result

End Function

